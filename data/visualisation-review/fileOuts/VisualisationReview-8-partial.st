Object subclass: #VRDatasetExamples	instanceVariableNames: ''	classVariableNames: ''	package: 'VisualisationReview-Examples'!!VRDatasetExamples commentStamp: '<historical>' prior: 0!I provide some basic examples and test for the Visualization Review dataset.!!VRDatasetExamples methodsFor: 'example' stamp: '1/19/2026 10:25'!allCSVFile	<gtExample>	^ self datasetFolder / 'dataset-review-vissoft-all.csv'! !!VRDatasetExamples methodsFor: 'example' stamp: '1/19/2026 10:56'!allPapers	<gtExample>	| papers |	"papers := self allPapersJSON collect: [ :data | VRPaper for: data ]."	papers := self vrDataset allPapers.	self assert: papers size equals: 346.	^ papers! !!VRDatasetExamples methodsFor: 'example' stamp: '1/19/2026 10:26'!allPapersJSON	<gtExample>	| json |	json := (CSV2JSON for: self allCSVFile contents) jsonObject.	self assert: json size equals: 346.	^ json! !!VRDatasetExamples methodsFor: 'example' stamp: '1/19/2026 10:25'!allPapersWrapped	<gtExample>	| papers |	papers := VRPaperGroup withAll: self allPapers.	self assert: papers size equals: 346.	^ papers! !!VRDatasetExamples methodsFor: 'example' stamp: '1/19/2026 10:25'!datasetFolder	<gtExample>	| folder |	folder := FileLocator gtResource / 'feenkcom' / 'gtoolkit-demos'			/ 'data' / 'visualisation-review'.	self assert: folder exists.	self assert: (folder allChildrenMatching: '*.csv') size equals: 2.	^ folder! !!VRDatasetExamples methodsFor: 'example' stamp: '1/19/2026 10:25'!paper327	<gtExample>	| paper |	paper := self allPapers detect: [ :p | p id = '327' ].	self assert: paper id equals: '327'.	self assert: paper isDesignStudy.	^ paper! !!VRDatasetExamples methodsFor: 'example' stamp: '1/19/2026 10:25'!selectedPapers	<gtExample>	| papers |	papers := self allPapers select: #isDesignStudy.	self assert: papers size equals: 65.	^ papers! !!VRDatasetExamples methodsFor: 'example' stamp: '1/19/2026 10:25'!selectedPapersJSON	<gtExample>	| json |	json := (CSV2JSON			for: (self datasetFolder / 'dataset-review-vissoft-selected.csv') contents)			jsonObject.	self assert: json size equals: 65.	^ json! !!VRDatasetExamples methodsFor: 'example' stamp: '1/19/2026 10:27'!vrDataset	<gtExample>	| dataset |	dataset := VRDataset			forAllCSV: self allCSVFile			andSelectedCSV: self selectedCSVFile.	^ dataset! !!VRDatasetExamples methodsFor: 'example' stamp: '1/19/2026 10:26'!selectedCSVFile	<gtExample>	^ self datasetFolder / 'dataset-review-vissoft-selected.csv'! !Object subclass: #VRDataset	instanceVariableNames: 'allJSON selectedJSON allDict'	classVariableNames: ''	package: 'VisualisationReview-Model'!!VRDataset commentStamp: '<historical>' prior: 0!I encapsulate a Visualization Review dataset, consisting of two CSV files that have been converted to JSON.

I also provide an interface to instantiate and retrieve the various domain entities latent in the CSV files.!!VRDataset methodsFor: 'accessing' stamp: '1/19/2026 10:19'!allJSON	^ allJSON! !!VRDataset methodsFor: 'accessing' stamp: '1/19/2026 10:19'!allJSON: anObject	allJSON := anObject! !!VRDataset methodsFor: 'accessing' stamp: '1/19/2026 10:19'!selectedJSON	^ selectedJSON! !!VRDataset methodsFor: 'accessing' stamp: '1/19/2026 10:20'!selectedJSON: anObject	selectedJSON := anObject! !!VRDataset methodsFor: 'accessing' stamp: '1/19/2026 10:38'!allDict	^ allDict		ifNil: [ allDict := (self allJSON					collect: [ :data | 						| paper |						paper := VRPaper for: data in: self.						paper id -> paper ]) asDictionary ]! !!VRDataset methodsFor: 'accessing' stamp: '1/19/2026 10:55'!allPapers	^ VRPaperGroup withAll: self allDict values! !!VRDataset methodsFor: 'views' stamp: '1/19/2026 10:57'!gtAllPapersFor: aView	<gtView>	^ aView forward		title: 'Papers';		object: [ self allPapers ];		view: #gtItemsFor:! !"-- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- "!VRDataset class	instanceVariableNames: ''!!VRDataset class commentStamp: '<historical>' prior: 0!I encapsulate a Visualization Review dataset, consisting of two CSV files that have been converted to JSON.

I also provide an interface to instantiate and retrieve the various domain entities latent in the CSV files.!!VRDataset class methodsFor: 'instance creation' stamp: '1/19/2026 10:22'!forAllCSV: aCSVFileReference andSelectedCSV: anotherCSVFileReference	^ self		forAllJSON: (CSV2JSON for: aCSVFileReference contents) jsonObject		andSelectedJSON: (CSV2JSON for: anotherCSVFileReference contents) jsonObject! !!VRDataset class methodsFor: 'instance creation' stamp: '1/19/2026 10:21'!forAllJSON: aJSON andSelectedJSON: anotherJSON	^ self new		allJSON: aJSON;		selectedJSON: anotherJSON;		yourself! !Object subclass: #VRPaperGroup	uses: TGtGroupWithItems	instanceVariableNames: ''	classVariableNames: ''	package: 'VisualisationReview-Model'!!VRPaperGroup commentStamp: '<historical>' prior: 0!I am a moldable collection data wrapper for groups of papers.!!VRPaperGroup methodsFor: 'views' stamp: '1/19/2026 09:49'!gtItemsFor: aView	<gtView>	^ aView columnedList		title: 'Papers';		items: [ self items ];		column: 'Index'			text: [ :eachItem :eachIndex | eachIndex asRopedText foreground: Color gray ]			width: 45;		column: 'Title' text: [ :each | each title ];		column: 'Type' text: [ :each | each type ];		actionUpdateButton! !"-- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- "!VRPaperGroup class	uses: TGtGroupWithItems classTrait	instanceVariableNames: ''!!VRPaperGroup class commentStamp: '<historical>' prior: 0!I am a moldable collection data wrapper for groups of papers.!Object subclass: #VRDomainEntity	instanceVariableNames: 'dataset'	classVariableNames: ''	package: 'VisualisationReview-Model'!!VRDomainEntity commentStamp: '<historical>' prior: 0!I am an asbtract class that provides common properties to all VR domain entities.!!VRDomainEntity methodsFor: 'accessing' stamp: '1/19/2026 10:35'!dataset	^ dataset! !!VRDomainEntity methodsFor: 'accessing' stamp: '1/19/2026 10:35'!dataset: anObject	dataset := anObject! !VRDomainEntity subclass: #VRPaper	instanceVariableNames: 'data'	classVariableNames: ''	package: 'VisualisationReview-Model'!!VRPaper commentStamp: '<historical>' prior: 0!I represent a paper (publication) in a Visualization Review dataset.!!VRPaper methodsFor: 'accessing' stamp: '1/13/2026 16:11'!data	^ data! !!VRPaper methodsFor: 'accessing' stamp: '1/13/2026 16:11'!data: anObject	data := anObject! !!VRPaper methodsFor: 'accessing' stamp: '1/13/2026 16:33'!id	^ self data at: 'ID'! !!VRPaper methodsFor: 'accessing' stamp: '1/13/2026 16:33'!type	^ self data at: 'Type'! !!VRPaper methodsFor: 'accessing' stamp: '1/13/2026 16:34'!title	^ self data at: 'Title'! !!VRPaper methodsFor: 'accessing' stamp: '1/19/2026 11:06'!authorNames	"Just the names ofthe authors, in order, not the author entities."	^ (self authorFields copyFrom: 1 to: (self data at: '# Authors') asNumber)		collect: [ :key | data at: key ]! !!VRPaper methodsFor: 'testing' stamp: '1/13/2026 16:53'!isDesignStudy	^ self type = 'Design Study'! !!VRPaper methodsFor: 'testing' stamp: '1/14/2026 16:26'!isSelected	^ self isDesignStudy! !!VRPaper methodsFor: 'printing' stamp: '1/13/2026 16:54'!printOn: aStream	aStream nextPutAll: self title! !!VRPaper methodsFor: 'views' stamp: '1/13/2026 17:18'!gtJsonObjectFor: aView	<gtView>	^ aView forward		title: 'JSON object';		priority: 20;		object: [ self data asGtJson ];		view: #gtJsonObjectFor:context:! !!VRPaper methodsFor: 'views' stamp: '1/14/2026 16:26'!gtSummaryFor: aView	<gtView>	^ aView columnedList		title: 'Summary';		priority: 10;		items: [ {{'ID'.					self id}.				{'Title'.					self title}.				{'Type'.					self type}.				{'# Authors'.					self data at: '# Authors'}.				{'# Pages'.					self data at: '# Pages'}.				{'Is selected'.					self isSelected}} ];		column: 'Key'			text: #first			width: 100;		column: 'Value' text: #second;		actionUpdateButton! !!VRPaper methodsFor: 'as yet unclassified' stamp: '1/19/2026 11:03'!authorFields	"All the author fields of the JSON, in order, including the original typos in the source CSV files."
	^ #(
		'First Author'
		'Second Author'
		'Third Author'
		'Fourth Author'
		'Fifth Author'
		'Sixth Author'
		'Seven Author'
		'Eighth Author'
		'Nineth Author'
		'Tenth Author'
		'Eleventh Author'
		'Twelfth Author'
	)! !"-- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- "!VRPaper class	instanceVariableNames: ''!!VRPaper class commentStamp: '<historical>' prior: 0!I represent a paper (publication) in a Visualization Review dataset.!!VRPaper class methodsFor: 'accessing' stamp: '1/19/2026 10:39'!for: data	self deprecated: 'Use #for:in: instead.'.	^ self new		data: data;		yourself! !!VRPaper class methodsFor: 'accessing' stamp: '1/19/2026 10:38'!for: jsonData in: aDataset	^ self new		data: jsonData;		dataset: aDataset		yourself! !