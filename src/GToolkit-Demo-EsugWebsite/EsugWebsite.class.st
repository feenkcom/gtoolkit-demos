Class {
	#name : #EsugWebsite,
	#superclass : #Object,
	#instVars : [
		'repo',
		'pages'
	],
	#classInstVars : [
		'instance'
	],
	#category : #'GToolkit-Demo-EsugWebsite-Model'
}

{ #category : #creation }
EsugWebsite class >> instance [
	^ instance ifNil: [ instance := self new ]
]

{ #category : #accessing }
EsugWebsite class >> pageNamed: aString [
	^ self instance pageNamed: aString
]

{ #category : #acccessing }
EsugWebsite class >> pages [
	^ self instance pages
]

{ #category : #creation }
EsugWebsite class >> reset [
	^ instance := nil
]

{ #category : #acccessing }
EsugWebsite class >> root [
	^ self instance root
]

{ #category : #accessing }
EsugWebsite >> gtPagesFor: aView [
	<gtView>
	^ aView columnedList
		title: 'Pages';
		priority: 10;
		items: [ self pages ];
		column: 'Page' text: [ :item | item pathString ] width: 400;
		column: 'Pillar links' text: [ :item | item pillarLinks size ] width: 80;
		column: 'Markdown links' text: [ :item | item markdownLinks size ] width: 80
]

{ #category : #accessing }
EsugWebsite >> gtSpotterForPageContents: aSearch [
	<gtSearch>
	^ aSearch list
		title: 'Page content';
		priority: 20;
		items: [ self pages ];
		itemsLimit: Float infinity;
		itemName: #contents;
		filterBySubstring
]

{ #category : #accessing }
EsugWebsite >> gtSpotterForPageNames: aSearch [
	<gtSearch>
	^ aSearch list
		title: 'Page names';
		priority: 10;
		items: [ self pages ];
		itemsLimit: Float infinity;
		itemName: #pathString;
		filterBySubstring
]

{ #category : #accessing }
EsugWebsite >> initializePages [
	^ ((self repo allChildrenMatching: '*.pillar')
		collect: [ :each | EsugWebPage for: each in: self ])
		sort: [ :a :b | 
			(a pathString occurrencesOf: $/) < (b pathString occurrencesOf: $/)
				or: [ (a pathString occurrencesOf: $/) = (b pathString occurrencesOf: $/)
						and: [ a pathString < b pathString ] ] ]
]

{ #category : #accessing }
EsugWebsite >> initializeRepo [
	"Clone the repo unless it already exists."

	| proc repoDir |
	repoDir := FileLocator imageDirectory / 'esug.github.io'.
	repoDir exists ifTrue: [ ^ repoDir ].
	proc := GtSubprocessWithInMemoryOutput new
			workingDirectory: FileLocator imageDirectory;
			command: 'git';
			arguments: {'clone'.
					self repoUrl};
			errorBlock: [ :theProc | self error: 'Git clone failed: ' , self repoUrl printString ].
	proc runAndWait.
	^ repoDir
]

{ #category : #accessing }
EsugWebsite >> pageNamed: aString [
	^ self pages detect: [ :page | page pathString = aString ]
]

{ #category : #accessing }
EsugWebsite >> pages [
	^ pages ifNil: [ pages := self initializePages ]
]

{ #category : #accessing }
EsugWebsite >> repo [
	^ repo ifNil: [ repo := self initializeRepo ]
]

{ #category : #constant }
EsugWebsite >> repoUrl [
	^ 'https://github.com/ESUG/esug.github.io'
]

{ #category : #accessing }
EsugWebsite >> root [
	^ self pages detect: [ :page | page pathString = self rootPath ]
]

{ #category : #constant }
EsugWebsite >> rootPath [
	^ '/index.pillar'
]
